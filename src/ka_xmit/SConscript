# -*- python -*-

xmitdTools = Split("""
logx
doxygen
xmlrpc
rapradar
boost_program_options
pmc730
""")

xmitdEnv = Environment(tools = ['default'] + xmitdTools)

xmitd_sources = Split("""
ka_xmitd.cpp
KaXmitter.cpp
""")

# We need KaPmc730 from kadrx
kaPmc730 = xmitdEnv.Object(target='KaPmc730.o', source='../kadrx/KaPmc730.cpp')

ka_xmitd = xmitdEnv.Program('ka_xmitd', xmitd_sources + kaPmc730)
Default(ka_xmitd)

xmitctlTools = Split("""
logx
doxygen
xmitclient
qt4
""")
xmitctlEnv = Environment(tools = ['default'] + xmitctlTools)
xmitctlEnv.EnableQt4Modules(['QtCore', 'QtGui'])

xmitctl_sources = Split("""
ka_xmitctl.cpp
KaXmitCtlMainWindow.cpp
qrc_KaXmitCtlMainWindow.cc
""")
ka_xmitctl = xmitctlEnv.Program('ka_xmitctl', xmitctl_sources)
Default(ka_xmitctl)

uifiles = Split("""
KaXmitCtlMainWindow.ui
""")
xmitctlEnv.Uic4(uifiles)

# This generates the rules for *.qrc -> qrc_*.cc
qrcfiles = Split("""
KaXmitCtlMainWindow.qrc
""")
xmitctlEnv.Qrc(qrcfiles)

headers = Split("""
KaXmitCtlMainWindow.h
KaXmitter.h
""")

html = xmitctlEnv.Apidocs(xmitd_sources + xmitctl_sources + headers, 
                   DOXYFILE_FILE = "Doxyfile")
Default(html)

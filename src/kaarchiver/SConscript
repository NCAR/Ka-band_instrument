import os

sources = []

tsarchiverSources = Split('''
TsArchiver.cpp
TsArchiverMain.cpp
TimeSeriesAdapter.cpp
''')
sources += tsarchiverSources

tsplaybackSources = Split('''
IwrfTsFilesReader.cpp
TsPlayback.cpp
TsPlaybackMain.cpp
TimeSeriesAdapter.cpp
''')
sources += tsplaybackSources

productplaybackSources = Split('''
CfRadialFilesReader.cpp
ProductPlayback.cpp
ProductPlaybackMain.cpp
ProductAdapter.cpp
''')
sources += productplaybackSources

ts2peifileSources = Split('''
IwrfTsFilesReader.cpp
Ts2PeiFile.cpp
''')
sources += ts2peifileSources

productarchiverSources = Split('''
ProductArchiver.cpp
ProductArchiverMain.cpp
ProductAdapter.cpp
''')
sources += productarchiverSources

headers = Split('''
CfRadialFilesReader.h
IwrfTsFilesReader.h
ProductAdapter.h
ProductArchiver.h
ProductPlayback.h
TimeSeriesAdapter.h
TsArchiver.h
TsPlayback.h
''')

inst = []


tstools = Split("""
boost_program_options
kadds
qtt_qtconfig
radd_archiver
rapradar
""")
tsenv = Environment(tools = ['default'] + tstools)

# build tsarchiver
tsarchiver = tsenv.Program('tsarchiver', tsarchiverSources)
inst += tsenv.InstallProgram(tsarchiver)
tsenv.Default(tsarchiver)

# build tsplayback
tsplayback = tsenv.Program('tsplayback', tsplaybackSources)
inst += tsenv.InstallProgram(tsplayback)
tsenv.Default(tsplayback)

# build ts2peifile
ts2peifile = tsenv.Program('ts2peifile', ts2peifileSources)
inst += tsenv.InstallProgram(ts2peifile)
tsenv.Default(ts2peifile)

# documentation
html = tsenv.Apidocs(sources + headers, DOXYFILE_FILE = "Doxyfile")
tsenv.AppendDoxref('tsArchiver')
tsenv.Default(html)




producttools = Split("""
boost_program_options
kadds
qtt_qtconfig
rapradx
xmlrpc
""")
productenv = Environment(tools = ['default'] + producttools)

# build productarchiver
productarchiver = productenv.Program('productarchiver', productarchiverSources)
inst += productenv.InstallProgram(productarchiver)
productenv.Default(productarchiver)

# build productplayback
productplayback = productenv.Program('productplayback', productplaybackSources)
inst += productenv.InstallProgram(productplayback)
productenv.Default(productplayback)

productenv.Alias('install', inst)
